!include "MUI2.nsh"
!include "ZipDLL.nsh"
!include "nsDialogs.nsh"
!include "nsDialogs_createTextMultiline.nsh"
!include "LogicLib.nsh"
!include "FileFunc.nsh"

Unicode true

;Strings
!define ProgramVersion "## VERSION ##"
!define CompanyName "Clarive"
!define CompanyNameLegal "Clarive Software, Inc."
!define ProgramName "Server"
!define ProgramNameVersion "${ProgramName} ${ProgramVersion}"
!define SupportLink "http://clarive.com"
!define HelpLink "http://clarive.com"

;Strings Registry
!define CompanyRegistryKey "Software\${CompanyName}"
!define ProgramRegistryKey "${CompanyRegistrykey}\${ProgramNameVersion}"
!define UninstallRegistryKey "Software\Microsoft\Windows\CurrentVersion\Uninstall\${ProgramNameVersion}"

!define WebService "Clarive Web Server - ${ProgramVersion}"
!define DispatcherService "Clarive Dispatcher - ${ProgramVersion}"

!define CygwinDist "## CYGWIN_DIST ##"
!define ClariveDist "## CLARIVE_DIST ##"

Name "${CompanyNameLegal} - ${ProgramName}"
OutFile "clarive_${ProgramVersion}_setup.exe"

InstallDir "C:\${CompanyName}\${ProgramName}"

; Save previously selected InstallDir to registry
InstallDirRegKey HKCU "${ProgramRegistryKey}" "InstallLocation"

BrandingText "${ProgramName} Installer"

RequestExecutionLevel admin

!define MUI_ICON "data\nsi\clarive.ico"
!define MUI_UNICON "data\nsi\clarive_uninstall.ico"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "data\nsi\header.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "data\nsi\header_uninstall.bmp"
!define MUI_HEADERIMAGE_LEFT
!define MUI_WELCOMEFINISHPAGE_BITMAP "data\nsi\welcome.bmp"

!define MUI_ABORTWARNING

!define MUI_LANGDLL_ALLLANGUAGES

!define MUI_LANGDLL_REGISTRY_ROOT "HKCU"
!define MUI_LANGDLL_REGISTRY_KEY "${ProgramRegistryKey}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "${ProgramName} Setup Language"

!define MUI_WELCOMEPAGE_TITLE "${ProgramName} Install/Setup"
!define MUI_WELCOMEPAGE_TEXT "This is the Clarive Server Install and Setup program. This program will install the Clarive Server components in this machine.$\r$\n$\r$\n\
For more information, please visit http://docs.clarive.com/setup/windows/.$\r$\n$\r$\n\
If you need help, please email support@clarive.com."

;--------------------------------
;Pages

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "LICENSE"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
Page custom ConfigurationPage ConfigurationPageLeave
Page custom PostInstall PostInstallLeave
Page custom ServiceStart

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;Variables
Var Label
Var Dialog
Var EnvName
Var EnvNameText
Var Config
Var ConfigText

Function ConfigurationPage
    !insertmacro MUI_HEADER_TEXT "Configure ${ProgramName}" "Set configuration variables."

    nsDialogs::Create 1018
    Pop $Dialog

    ${If} $Dialog == error
        Abort
    ${EndIf}

    ${NSD_CreateLabel} 0 0 100% 12u "Environment file name:"
    Pop $Label

    ${NSD_CreateText} 0 12u 100% 12u "test"
    Pop $EnvNameText

    ${NSD_CreateLabel} 0 28u 100% 12u "Environment configuration (YAML format):"
    Pop $Label

    ${NSD_CreateTextMultiline} 0 40u 100% -40u "lang: en_US.UTF-8$\r$\nport: 3000$\r$\n$\r$\nmongo:$\r$\n    dbname: test"
    Pop $ConfigText

    nsDialogs::Show
FunctionEnd

Function ConfigurationPageLeave

    ${NSD_GetText} $EnvNameText $EnvName
    ${NSD_GetText} $ConfigText $Config

    ${If} $EnvName == ""
        MessageBox MB_OK "Please enter Environment file name"
        Abort
    ${EndIf}

    ${If} $Config == ""
        MessageBox MB_OK "Please enter Environment configuration"
        Abort
    ${EndIf}

FunctionEnd

Var StartWebServiceCheckbox
Var StartWebServiceCheckbox_State
Var StartDispatcherServiceCheckbox
Var StartDispatcherServiceCheckbox_State

Var InstallMenuShortCutsCheckbox
Var InstallMenuShortCutsCheckbox_State

!macro ExecLog Command

    ;LogText ${Command}

    nsExec::ExecToStack '${Command}'
    ;ExecWait '${Command}'

    Pop $0
    Pop $1

    ;LogText "ExitCode=$0"
    ;LogText "Output=$1"

!macroend

!macro CreateServiceInstallBatchFile Type EnvName ServiceName

    Delete "$INSTDIR\bin\service-${Type}-${EnvName}-install.bat"
    FileOpen $0 "$INSTDIR\bin\service-${Type}-${EnvName}-install.bat" w
    FileWrite $0 '"$INSTDIR\app\bin\cygrunsrv" --remove $\"${ServiceName} (${EnvName})$\"$\r$\n'
    FileWrite $0 '"$INSTDIR\app\bin\cygrunsrv" --install $\"${ServiceName} (${EnvName})$\"                           \
    --env $\"TZ=UTC$\"                          \
    --path /opt/clarive/clarive/bin/cla                   \
    --args $\"${Type}-start --env ${EnvName}$\"                      \
    --stdout $\"$INSTDIR\app\opt\clarive\logs\service.${Type}.${EnvName}.stdout.log$\" \
    --stderr $\"$INSTDIR\app\opt\clarive\logs\service.${Type}.${EnvName}.stderr.log$\"'
    FileClose $0

!macroend

!macro CreateServiceActionBatchFile Type EnvName ServiceName Action

    Delete "$INSTDIR\bin\service-${Type}-${EnvName}-${Action}.bat"
    FileOpen $0 "$INSTDIR\bin\service-${Type}-${EnvName}-${Action}.bat" w
    FileWrite $0 '"$INSTDIR\app\bin\cygrunsrv" --${Action} $\"${ServiceName} (${EnvName})$\"'
    FileClose $0

!macroend

Function PostInstall
    LogSet on

    !insertmacro MUI_HEADER_TEXT "${ProgramName} Install/Setup is finished" ""

    nsDialogs::Create 1018
    Pop $Dialog

    ${NSD_CreateCheckbox} 0 0 100% 12u "Start &Web Service"
    Pop $StartWebServiceCheckbox
    ${NSD_Check} $StartWebServiceCheckbox

    ${NSD_CreateCheckbox} 0 12u 100% 24u "Start &Dispatcher Service"
    Pop $StartDispatcherServiceCheckbox
    ${NSD_Check} $StartDispatcherServiceCheckbox

    ${NSD_CreateCheckbox} 0 48u 100% 12u "Install Start &Menu Shortcuts"
    Pop $InstallMenuShortCutsCheckbox
    ${NSD_Check} $InstallMenuShortCutsCheckbox

    nsDialogs::Show

    CreateDirectory $INSTDIR\bin

    Delete "$INSTDIR\app\opt\clarive\config\$EnvName.yml"
    FileOpen $8 "$INSTDIR\app\opt\clarive\config\$EnvName.yml" w
    FileWrite $8 "$Config"
    FileClose $8

    FileOpen $9 "$INSTDIR\Shell.bat" w
    FileWrite $9 "@echo off$\r$\n"
    FileWrite $9 "$\r$\n"
    FileWrite $9 "C:$\r$\n"
    FileWrite $9 "chdir $INSTDIR\app\bin$\r$\n"
    FileWrite $9 "$\r$\n"
    FileWrite $9 "SET PATH=$INSTDIR\app\opt\clarive\clarive\bin;$INSTDIR\app\opt\clarive\local\bin;%PATH%$\r$\n"
    FileWrite $9 "bash --login -i$\r$\n"
    FileClose $9

    !insertmacro CreateServiceInstallBatchFile "web" "$EnvName" "${WebService}"
    !insertmacro CreateServiceActionBatchFile "web" "$EnvName" "${WebService}" "start"
    !insertmacro CreateServiceActionBatchFile "web" "$EnvName" "${WebService}" "stop"
    !insertmacro CreateServiceActionBatchFile "web" "$EnvName" "${WebService}" "remove"

    !insertmacro CreateServiceInstallBatchFile "disp" "$EnvName" "${DispatcherService}"
    !insertmacro CreateServiceActionBatchFile "disp" "$EnvName" "${DispatcherService}" "start"
    !insertmacro CreateServiceActionBatchFile "disp" "$EnvName" "${DispatcherService}" "stop"
    !insertmacro CreateServiceActionBatchFile "disp" "$EnvName" "${DispatcherService}" "remove"

    !insertmacro ExecLog '"$INSTDIR\bin\service-web-$EnvName-remove.bat"'
    !insertmacro ExecLog '"$INSTDIR\bin\service-web-$EnvName-install.bat"'

    !insertmacro ExecLog '"$INSTDIR\bin\service-disp-$EnvName-remove.bat"'
    !insertmacro ExecLog '"$INSTDIR\bin\service-disp-$EnvName-install.bat"'

    CreateShortCut "$INSTDIR\Server Config Files.lnk" "$INSTDIR\app\opt\clarive\config"
    CreateShortCut "$INSTDIR\Server Log Files.lnk" "$INSTDIR\app\opt\clarive\logs"
    CreateShortCut "$INSTDIR\Server Jobs.lnk" "$INSTDIR\app\opt\clarive\jobs"
    CreateShortCut "$INSTDIR\Server Plugins.lnk" "$INSTDIR\app\opt\clarive\plugins"

    WriteINIStr "$INSTDIR\Documentation.URL" "InternetShortcut" "URL" "http://docs.clarive.com"

    ;Store installation folder
    WriteRegStr HKCU "${ProgramRegistryKey}" "InstallLocation" $INSTDIR
    WriteRegStr HKCU "${ProgramRegistryKey}" "DisplayVersion" "${ProgramVersion}"

    ;Create uninstaller
    WriteUninstaller "$INSTDIR\Uninstall.exe"

    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "DisplayName" "${CompanyName}: ${ProgramNameVersion}"
    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "DisplayIcon" "$INSTDIR\clarive.ico"
    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "DisplayVersion" "${ProgramVersion}"
    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "Publisher" "${CompanyNameLegal}"
    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "HelpLink" "${HelpLink}"
    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "URLInfoAbout" "${SupportLink}"

    WriteRegStr HKLM "${UninstallRegistryKey}" \
                     "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
    ;WriteRegStr HKLM "${UninstallRegistryKey}" \
    ;                 "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"

    ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
    IntFmt $0 "0x%08X" $0
    WriteRegDWORD HKLM "${UninstallRegistryKey}" "EstimatedSize" "$0"

FunctionEnd

Function PostInstallLeave

    ${NSD_GetState} $StartWebServiceCheckbox $StartWebServiceCheckbox_State
    ${NSD_GetState} $StartDispatcherServiceCheckbox $StartDispatcherServiceCheckbox_State
    ${NSD_GetState} $InstallMenuShortCutsCheckbox $InstallMenuShortCutsCheckbox_State

FunctionEnd

Function ServiceStart

    nsDialogs::Create 1018
    Pop $Dialog

    ${NSD_CreateLabel} 0 0 100% 48u "Installation is completed.$\r$\n$\r$\nVisit the http://localhost:3000 (or use other port configured previously)."
    Pop $Label

    nsDialogs::Show

    ${If} $StartWebServiceCheckbox_State == ${BST_CHECKED}

        !insertmacro ExecLog '"$INSTDIR\bin\service-web-$EnvName-start.bat"'

    ${EndIf}

    ${If} $StartDispatcherServiceCheckbox_State == ${BST_CHECKED}

        !insertmacro ExecLog '"$INSTDIR\bin\service-disp-$EnvName-start.bat"'

    ${EndIf}

    ${If} $InstallMenuShortCutsCheckbox_State == ${BST_CHECKED}

        SetShellVarContext all

        ; Menu ShortCuts
        CreateDirectory "$SMPROGRAMS\${CompanyName}"
        CreateDirectory "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Bin.lnk" "$INSTDIR\bin"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Server Config Files.lnk" "$INSTDIR\app\opt\clarive\config"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Server Log Files.lnk" "$INSTDIR\app\opt\clarive\logs"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Server Jobs.lnk" "$INSTDIR\app\opt\clarive\jobs"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Server Plugins.lnk" "$INSTDIR\app\opt\clarive\plugins"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Documentation.lnk" "$INSTDIR\Documentation.URL"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Shell.lnk" "$INSTDIR\Shell.bat"
        CreateShortCut "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}\Uninstall.lnk" "$INSTDIR\Uninstall.exe"

    ${EndIf}

FunctionEnd

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English" ;first language is the default language
!insertmacro MUI_LANGUAGE "Spanish"

;--------------------------------
;Reserve Files

  ;If you are using solid compression, files that are required before
  ;the actual installation should be stored first in the data block,
  ;because this will make your installer start faster.

!insertmacro MUI_RESERVEFILE_LANGDLL

;--------------------------------
;Installer Sections

Section "Clarive Server" SectionServer

    SetOutPath "$INSTDIR"

    File /oname=clarive.ico "${MUI_ICON}"

    File "${CygwinDist}"
    File "${ClariveDist}"

    !insertmacro ZIPDLL_EXTRACT "${CygwinDist}" "$INSTDIR\" "<ALL>"

    Delete "$INSTDIR\${CygwinDist}"

    CreateDirectory $INSTDIR\app\opt\clarive
    CreateDirectory $INSTDIR\app\opt\clarive\config
    CreateDirectory $INSTDIR\app\opt\clarive\plugins
    CreateDirectory $INSTDIR\app\opt\clarive\logs
    CreateDirectory $INSTDIR\app\opt\clarive\jobs
    !insertmacro ZIPDLL_EXTRACT "${ClariveDist}" "$INSTDIR\app\opt\clarive\" "<ALL>"

    Delete "$INSTDIR\${ClariveDist}"

SectionEnd

;--------------------------------
;Installer Functions

Function .onInit
    IfSilent done 0 ; Jump to done if in silent mode

    !insertmacro MUI_LANGDLL_DISPLAY

    ReadRegStr $R0 HKLM "${UninstallRegistryKey}" "UninstallString"
    StrCmp $R0 "" done

    MessageBox MB_YESNOCANCEL|MB_ICONEXCLAMATION \
    "${ProgramNameVersion} is already installed. Remove previous installation? $\n$\nClick `Yes` to remove, `No` to install without removing, or `Cancel` to cancel this upgrade." \
    IDYES uninst IDNO done
    Abort

uninst:
    ClearErrors
    ExecWait '$R0 _?=$INSTDIR'

    IfErrors no_remove_uninstaller done
    no_remove_uninstaller:

done:

FunctionEnd

;--------------------------------
;Descriptions

  ;USE A LANGUAGE STRING IF YOU WANT YOUR DESCRIPTIONS TO BE LANGAUGE SPECIFIC

  ;Assign descriptions to sections
    !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
        !insertmacro MUI_DESCRIPTION_TEXT ${SectionServer} "Clarive Server files."
    !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "Uninstall"

    !insertmacro ExecLog '"$INSTDIR\bin\service-web-$EnvName-stop.bat"'
    !insertmacro ExecLog '"$INSTDIR\bin\service-web-$EnvName-remove.bat"'

    !insertmacro ExecLog '"$INSTDIR\bin\service-disp-$EnvName-stop.bat"'
    !insertmacro ExecLog '"$INSTDIR\bin\service-disp-$EnvName-remove.bat"'

    Delete "$INSTDIR\Uninstall.exe"

    RMDir /r "$INSTDIR"

    SetShellVarContext all
    RMDir /r "$SMPROGRAMS\${CompanyName}\${ProgramNameVersion}"
    RMDir "$SMPROGRAMS\${CompanyName}"

    DeleteRegKey HKCU "${ProgramRegistryKey}"

    DeleteRegKey HKLM "${UninstallRegistryKey}"

SectionEnd

;--------------------------------
;Uninstaller Functions

Function un.onInit

    !insertmacro MUI_UNGETLANGUAGE

FunctionEnd
